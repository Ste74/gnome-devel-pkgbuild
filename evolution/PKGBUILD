# $Id: PKGBUILD 264794 2016-04-12 22:14:16Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=evolution
pkgname=('evolution' 'evolution-bogofilter' 'evolution-spamassassin')
pkgver=3.23.2
pkgrel=1
pkgdesc="Manage your email, contacts and schedule"
arch=(i686 x86_64)
license=(GPL)
makedepends=(intltool itstool docbook-xsl networkmanager bogofilter spamassassin highlight gnome-desktop evolution-data-server libcanberra libpst libytnef desktop-file-utils dconf gtkspell3 webkitgtk libcryptui gnome-common gtk-doc)
options=('!emptydirs')
url=https://wiki.gnome.org/Apps/Evolution
source=(https://download.gnome.org/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('ae30dad016b53708164858ab559ca10cea30238d872e8a90e0c2c4c99820e5c3')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgbase-$pkgver

  cmake -G "Unix Makefiles" 					\
        -DCMAKE_BUILD_TYPE=Release 				\
        -DCMAKE_INSTALL_PREFIX=/usr 				\
	-DSYSCONF_INSTALL_DIR=/etc 				\
	-DLIBEXEC_INSTALL_DIR=/usr/lib				\
	-DWITH_NSS=ON						\
	-DWITH_OPENLDAP=ON					\
	-DENABLE_SMIME=ON					\
	-DENABLE_SCHEMAS_COMPILE=OFF				\
	-DENABLE_GTK_DOC=ON					\

  make
}

package_evolution() {
  depends=(gnome-desktop evolution-data-server libcanberra libpst libytnef
           desktop-file-utils dconf gtkspell3 libcryptui webkitgtk)
  install=evolution.install
  optdepends=('highlight: text highlight plugin'
              'evolution-spamassassin: Spamassassin spam check plugin'
              'evolution-bogofilter: Bogofilter spam check plugin')
  groups=('gnome-extra')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir ../plugins
  mv "$pkgdir/usr/lib/evolution/modules/module-bogofilter.so" ../plugins/
  mv "$pkgdir/usr/lib/evolution/modules/module-spamassassin.so" ../plugins/
  mv "$pkgdir/usr/share/appdata/evolution-bogofilter.metainfo.xml" ../plugins/
  mv "$pkgdir/usr/share/appdata/evolution-spamassassin.metainfo.xml" ../plugins/
}


package_evolution-bogofilter() {
  depends=("evolution=$pkgver" 'bogofilter')
  install -Dm755 plugins/module-bogofilter.so "$pkgdir/usr/lib/evolution/modules/module-bogofilter.so"
  install -Dm644 plugins/evolution-bogofilter.metainfo.xml "$pkgdir/usr/share/appdata/evolution-bogofilter.metainfo.xml"
}

package_evolution-spamassassin() {
  depends=("evolution=$pkgver" 'spamassassin')
  install -Dm755 plugins/module-spamassassin.so "$pkgdir/usr/lib/evolution/modules/module-spamassassin.so"
  install -Dm644 plugins/evolution-spamassassin.metainfo.xml "$pkgdir/usr/share/appdata/evolution-spamassassin.metainfo.xml"
}
