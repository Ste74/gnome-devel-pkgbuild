# $Id: PKGBUILD 264771 2016-04-12 21:46:30Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gspell
pkgver=1.4.1
pkgrel=1
pkgdesc="a flexible API to implement the spell checking in a GTK+ application"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/gspell"
license=('GPL2')
depends=('gtk3' 'enchant' 'libxml2' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=("https://download.gnome.org/sources/gspell/${pkgver:0:3}/gspell-${pkgver}.tar.xz")
sha256sums=('442f88fecd7f9e00a6a9d0af2d762744dd3c119217ff11d6739500f75b0f10be')

build() {
  cd "$pkgname-$pkgver"
  ./configure  --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
