# $Id: PKGBUILD 143892 2015-10-13 08:13:45Z heftig $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.19.4
pkgrel=1
pkgdesc="Simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('clutter-gtk' 'gtk-vnc' 'libarchive' 'libosinfo' 'libsoup' 'libvirt-glib'
         'spice-gtk3' 'libtracker-sparql' 'qemu' 'cdrkit' 'mtools' 'dconf'
         'gnome-themes-standard' 'libusb' 'libgudev')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
md5sums=('8a8fabd3b67196923eb32f13fbc7a730')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --enable-smartcard
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
